// Lark grammar for "expression"-based alphas

?value: abs
      | log
      | sign
      | greaterthan
      | lessthan
      | equals
      | logicalor
      | ternary
      | scale
      | signedpower
      | decay_linear
      | ts_max
      | ts_min
      | ts_argmax
      | ts_argmin
      | ts_rank
      | max
      | min
      | stddev
      | close
      | opens
      | high
      | low
      | volume
      | returns
      | adv
      | vwap
      | delay
      | correlation
      | covariance
      | indneutralize
      | delta
      | div
      | mult
      | minus
      | plus
      | powerof
      | rank
      | sum
      | product
      | ESCAPED_STRING
      | "(" value ")"
      | neg
      | number
      | cap
      | SIGNED_NUMBER     -> number
      | factory


delay: "delay" "(" value "," SIGNED_NUMBER ")"
delta: "delta" "(" value "," SIGNED_NUMBER ")"

correlation: "correlation" "(" value "," value "," SIGNED_NUMBER ")"
covariance: "covariance" "(" value "," value "," SIGNED_NUMBER ")"

factory: "factory" "(" ESCAPED_STRING ["," ESCAPED_STRING ] ")"

close: "close"
opens: "opens"
high: "high"
low: "low"
volume: "volume"
returns: "returns"
vwap: "vwap"
adv: "adv" SIGNED_NUMBER
cap: "cap"

number: SIGNED_NUMBER

ts_max: "ts_max" "(" value "," SIGNED_NUMBER ")"
ts_min: "ts_min" "(" value "," SIGNED_NUMBER ")"
ts_argmax: "ts_argmax" "(" value "," SIGNED_NUMBER ")"
ts_argmin: "ts_argmin" "(" value "," SIGNED_NUMBER ")"
ts_rank: "ts_rank" "(" value "," SIGNED_NUMBER ")"
stddev: "stddev" "(" value "," SIGNED_NUMBER ")"
indneutralize: "indneutralize" "(" value ["," INDCLASS] ")"

// not what paper says; this is element by element min
max: "max" "(" value "," value ")"
min: "min" "(" value "," value ")"

div: value "/" value
minus: value "-" value
plus: value "+" value
mult: value "*" value
powerof: value "^" value

abs: "abs" "(" value ")"
log: "log" "(" value ")"
sign: "sign" "(" value ")"
greaterthan: value ">" value
lessthan: value "<" value
equals: value "==" value
logicalor: value "||" value
ternary: value "?" value ":" value
scale: "scale" "(" value ")"
signedpower: "signedpower" "(" value "," value ")"
decay_linear: "decay_linear" "(" value "," SIGNED_NUMBER ")"

neg: "-" value
rank: "rank" "(" value ")"
sum: "sum" "(" value "," SIGNED_NUMBER ")"
product: "product" "(" value "," SIGNED_NUMBER ")"

INDCLASS: ("IndClass.industry" | "IndClass.subindustry" | "IndClass.sector")

%import common.SIGNED_NUMBER
%import common.ESCAPED_STRING
%import common.WS
%ignore WS